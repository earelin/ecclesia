buildscript {
    repositories {
        maven {
            url "https://plugins.gradle.org/m2/"
        }
        jcenter()
    }
    dependencies {
        classpath "gradle.plugin.com.github.spotbugs:spotbugs-gradle-plugin:${gradleSpotbugsVersion}"
    }
}

plugins {
    id "io.spring.dependency-management" version "1.0.6.RELEASE"
}

subprojects {

    buildscript {
        repositories {
            maven {
                url "https://repo.spring.io/libs-milestone"
            }
            maven {
                url "https://plugins.gradle.org/m2/"
            }
        }
    }

    apply plugin: 'checkstyle'
    apply plugin: 'jacoco'
    apply plugin: 'java'
    apply plugin: 'pmd'
    apply plugin: 'com.github.spotbugs'

    sourceCompatibility = 1.8
    targetCompatibility = 1.8

    compileJava.options*.compilerArgs = ["-Xlint:all"]

    compileTestJava.options*.compilerArgs = ["-Xlint:all"]

    repositories {
        jcenter()
        maven {
            url "https://repo.spring.io/libs-milestone"
        }
    }

    dependencies {
        checkstyle "com.puppycrawl.tools:checkstyle:${checkstyleToolVersion}"
    }

    checkstyle {
        configFile = file("${project.rootDir}/checkstyle.xml")
    }

    jacocoTestReport {
        reports {
            xml.enabled = true
            html.enabled = true
        }
    }
    check.dependsOn jacocoTestReport

    pmd {
        ignoreFailures = true
        sourceSets = [sourceSets.main]
    }

    spotbugs {
        sourceSets = [sourceSets.main]
        ignoreFailures = true
        effort = "max"
        reportLevel = "low"
    }
}
